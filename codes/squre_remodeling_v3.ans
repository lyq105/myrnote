FINISH  ! Make sure we are at BEGIN level   
/CLEAR,NOSTART  ! Clear model since no SAVE found   

FINISH  

!!  计算中的参数

B = 1.0
C = 100
ka = 0.25e3
gamma = 2
rho_init = 0.8e3
rho_0 = 0.01e3
rho_cb = 1.74e3
possion_ratio = 0.3
ediv = 100
square_lenth = 0.1

!! 定义上表面的表载荷

*dim,toppres,table,2,1,,x, ,  
*set,toppres(1,0) , 0
*set,toppres(1,1) , 10e6   
*set,toppres(2,0) , square_lenth

/prep7  
rectng,0,square_lenth,0,square_lenth, 

!*  选取单元，设置单元参数  
et,1,plane183
!et,1,plane42
  
keyopt,1,1,0
keyopt,1,2,0
keyopt,1,3,0
keyopt,1,5,0
keyopt,1,6,0

!! 剖分网格
mshape,0,2d 
mshkey,1
asel,all 
esize, ,ediv, 
amesh,all   

!# 准备数据
!! 获取最大单元数
*get,emax,elem,,num,max
*get,nmax,node,,num,max
*dim,rho,,emax
*dim,esed,,emax

!!  设置材料参数
*do, eindex , 1,emax,1
    rho(eindex) =  rho_init    !! 设定每一个单元的密度初始值
		mp,prxy,eindex, possion_ratio !! 设定每一个单元的poisson‘s ratio
    mp,ex,eindex,C*rho(eindex)**gamma
		mpchg,eindex,eindex
*enddo
finish

!!   清空数据文件

*cfopen,density,plt
*cfclos
*cfopen,obj,plt
*cfclos

!! 时间步
*do,t,0,200,1
    /sol
    lsel,s,line,,1
    dl,all, ,uy,0  

    lsel,s,line,,3
    sfl,all,pres, %toppres%

    allsel,all
    solve   
    finish

    !! 画图
		/post1
		/show,jpeg
		plnsol, u,y, 0,1.0 
		/show,close
		/rename,file000,jpg,,uy%t%,jpg
		finish

    obj_function = 0

!! 单元循环,更新单元材料参数
    *do,eindex,1,emax,1
         !! 计算单元N的应变能密度
         *get,esen,elem,eindex,tene    !提取单元应变，help中写的是“stiffness energy”
         *get,evol,elem,eindex,volu    !提取单元的体积
         *set,esed(eindex),esen/evol      !得到单元的应变能密度
         
         !! 更新密度 如果密度值 在rho_0 和rho_cb 之间 则更新密度。
         *if,rho(eindex),lt,rho_0,then
            rho(eindex) = rho_0
         *endif
         
         *if,rho(eindex),gt,rho_cb,then
            rho(eindex) = rho_cb
         *endif

         *if,rho(eindex),gt,rho_0,and,rho(eindex),lt,rho_cb,then 
            !! 密度更新方程
            rho(eindex) = rho(eindex) + B*(esed(eindex)/rho(eindex) - ka)   
	          !!计算目标函数
            obj_function = obj_function + abs(esed(eindex)/rho(eindex) - ka)
         *endif
         
         !! 更新单元的材料参数 
         /prep7
         mp,ex,eindex,C*rho(eindex)**gamma   !! Young模量
    *enddo !! 单元循环

    obj_function = obj_function/emax

    !! 输出目标函数值

    *cfopen,obj,plt,,append
    *vwrite,t,obj_function
%12.8f %12.8f
    *cfclos

!!  打开密度数据文件
*cfopen,density,plt,,append

*vwrite,ediv+1,ediv+1
zone i=%8I,j=%8I
*do,i,1,ediv,1
	*do,j,1,ediv,1
	    ielem = (i-1)*ediv + j
			ildnode = nelem(ielem,1)
      p = rho(ielem)*1e-3
      *vwrite,nx(ildnode),ny(ildnode),esed(ielem)*1e-6/p,p
%12.8f %12.8f %18.8f %18.8f
  *enddo
	    ielem = (i-1)*ediv + j
			ildnode = nelem(ielem,2)
      p = rho(ielem)*1e-3
      *vwrite,nx(ildnode),ny(ildnode),esed(ielem)*1e-6/p,p
%12.8f %12.8f %18.8f %18.8f

*enddo

*do,j,1,ediv,1
    ielem = (ediv-1)*ediv + j
    ildnode = nelem(ielem,4)
    *vwrite,nx(ildnode),ny(ildnode),esed(ielem)*1e-6/p,p
%12.8f %12.8f %18.8f %18.8f
*enddo

ielem = (ediv-1)*ediv + j
ildnode = nelem(ielem,3)
*vwrite,nx(ildnode),ny(ildnode),esed(ielem)*1e-6/p,p
%12.8f %12.8f %18.8f %18.8f
*cfclos !! 结束文件输入

*enddo !! 时间步

FINISH  

